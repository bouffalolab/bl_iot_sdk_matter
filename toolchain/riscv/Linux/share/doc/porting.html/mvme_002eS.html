<html lang="en">
<head>
<title>mvme.S - Embed with GNU</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name="description" content="Embed with GNU">
<meta name="generator" content="makeinfo 4.13">
<link title="Top" rel="start" href="index.html#Top">
<link rel="up" href="Code-Listings.html#Code-Listings" title="Code Listings">
<link rel="prev" href="glue_002ec.html#glue_002ec" title="glue.c">
<link rel="next" href="io_002ec.html#io_002ec" title="io.c">
<link href="http://www.gnu.org/software/texinfo/" rel="generator-home" title="Texinfo Homepage">
<meta http-equiv="Content-Style-Type" content="text/css">
<style type="text/css"><!--
  pre.display { font-family:inherit }
  pre.format  { font-family:inherit }
  pre.smalldisplay { font-family:inherit; font-size:smaller }
  pre.smallformat  { font-family:inherit; font-size:smaller }
  pre.smallexample { font-size:smaller }
  pre.smalllisp    { font-size:smaller }
  span.sc    { font-variant:small-caps }
  span.roman { font-family:serif; font-weight:normal; } 
  span.sansserif { font-family:sans-serif; font-weight:normal; } 
--></style>
</head>
<body>
<div class="node">
<a name="mvme.S"></a>
<a name="mvme_002eS"></a>
<p>
Next:&nbsp;<a rel="next" accesskey="n" href="io_002ec.html#io_002ec">io.c</a>,
Previous:&nbsp;<a rel="previous" accesskey="p" href="glue_002ec.html#glue_002ec">glue.c</a>,
Up:&nbsp;<a rel="up" accesskey="u" href="Code-Listings.html#Code-Listings">Code Listings</a>
<hr>
</div>

<h3 class="section">A.4 I/O assembler code sample</h3>

<pre class="example">     /*
      * mvme.S -- board support for m68k
      */
     
     	.title "mvme.S for m68k-coff"
     
     /* These are predefined by new versions of GNU cpp.  */
     
     #ifndef __USER_LABEL_PREFIX__
     #define __USER_LABEL_PREFIX__ _
     #endif
     
     #ifndef __REGISTER_PREFIX__
     #define __REGISTER_PREFIX__
     #endif
     
     /* ANSI concatenation macros.  */
     
     #define CONCAT1(a, b) CONCAT2(a, b)
     #define CONCAT2(a, b) a ## b
     
     /* Use the right prefix for global labels.  */
     
     #define SYM(x) CONCAT1 (__USER_LABEL_PREFIX__, x)
     
     /* Use the right prefix for registers.  */
     
     #define REG(x) CONCAT1 (__REGISTER_PREFIX__, x)
     
     #define d0 REG (d0)
     #define d1 REG (d1)
     #define d2 REG (d2)
     #define d3 REG (d3)
     #define d4 REG (d4)
     #define d5 REG (d5)
     #define d6 REG (d6)
     #define d7 REG (d7)
     #define a0 REG (a0)
     #define a1 REG (a1)
     #define a2 REG (a2)
     #define a3 REG (a3)
     #define a4 REG (a4)
     #define a5 REG (a5)
     #define a6 REG (a6)
     #define fp REG (fp)
     #define sp REG (sp)
     #define vbr REG (vbr)
     
     	.align	2
     	.text
     	.global SYM (_exit)
     	.global SYM (outln)
     	.global SYM (outbyte)
     	.global SYM (putDebugChar)
     	.global SYM (inbyte)
     	.global SYM (getDebugChar)
     	.global SYM (havebyte)
     	.global SYM (exceptionHandler)
     
     	.set	vbr_size, 0x400
     	.comm	SYM (vbr_table), vbr_size
     
     /*
      * inbyte -- get a byte from the serial port
      *	d0 - contains the byte read in
      */
     	.align	2
     SYM (getDebugChar):		/* symbol name used by m68k-stub */
     SYM (inbyte):
     	link	a6, #-8
     	trap 	#15
     	.word	inchr
     	moveb 	sp@, d0
     	extbl	d0
     	unlk	a6
     	rts
     
     /*
      * outbyte -- sends a byte out the serial port
      *	d0 - contains the byte to be sent
      */
     	.align	2
     SYM (putDebugChar):		/* symbol name used by m68k-stub */
     SYM (outbyte):
     	link	fp, #-4
      	moveb	fp@(11), sp@
     	trap 	#15
     	.word	outchr
     	unlk	fp
     	rts
     
     /*
      * outln -- sends a string of bytes out the serial port with a CR/LF
      *	a0 - contains the address of the string's first byte
      *	a1 - contains the address of the string's last byte
      */
     	.align	2
     SYM (outln):
     	link	a6, #-8
     	moveml	a0/a1, sp@
     	trap 	#15
     	.word 	outln
     	unlk	a6
     	rts
     
     /*
      * outstr -- sends a string of bytes out the serial port without a CR/LF
      *	a0 - contains the address of the string's first byte
      *	a1 - contains the address of the string's last byte
      */
     	.align	2
     SYM (outstr):
     	link	a6, #-8
     	moveml	a0/a1, sp@
     	trap 	#15
     	.word 	outstr
     	unlk	a6
     	rts
     
     /*
      * havebyte -- checks to see if there is a byte in the serial port,
      *	     returns 1 if there is a byte, 0 otherwise.
      */
     SYM (havebyte):
     	trap 	#15
     	.word	instat
     	beqs	empty
     	movel 	#1, d0
     	rts
     empty:
     	movel	#0, d0
     	rts
     
     /*
      * These constants are for the MVME-135 board's boot monitor. They
      * are used with a TRAP #15 call to access the monitor's I/O routines.
      * they must be in the word following the trap call.
      */
     	.set inchr, 0x0
     	.set instat, 0x1
     	.set inln, 0x2
     	.set readstr, 0x3
     	.set readln, 0x4
     	.set chkbrk, 0x5
     
     	.set outchr, 0x20
     	.set outstr, 0x21
     	.set outln, 0x22
     	.set write, 0x23
     	.set writeln, 0x24
     	.set writdln, 0x25
     	.set pcrlf, 0x26
     	.set eraseln, 0x27
     	.set writd, 0x28
     	.set sndbrk, 0x29
     
     	.set tm_ini, 0x40
     	.set dt_ini, 0x42
     	.set tm_disp, 0x43
     	.set tm_rd, 0x44
     
     	.set redir, 0x60
     	.set redir_i, 0x61
     	.set redir_o, 0x62
     	.set return, 0x63
     	.set bindec, 0x64
     
     	.set changev, 0x67
     	.set strcmp, 0x68
     	.set mulu32, 0x69
     	.set divu32, 0x6A
     	.set chk_sum, 0x6B
     
</pre>
   </body></html>

